=head3 Context

Z<context_philosophy>

X<context>

Spoken languages have a notion of I<context> where the meaning of a word or
phrase depends on its surroundings.  So does Perl.  This may sound strange and
foreign in a programming language--but if you understand this paragraph, you
already understand how Perlish contexts work in English.

One type of context in Perl means that certain operators have different
behavior if you want zero, one, or many results.  It's possible that a specific
construct in Perl will do something different if you say "Fetch me zero
results; I don't care about any" than if you say "Fetch me one result" or
"Fetch me many results."

Likewise, certain contexts make it clear that you expect a numeric value, a
string value, or a value that's either true or false.

Context can be tricky if you try to write or read Perl code as a series of
single expressions which stand apart from from their environments.  You may
find yourself slapping your forehead after a long debugging session when you 
discover that your assumptions about context were incorrect.  However, if you're
cognizant of contexts, they can make your code clearer, more concise, and more
flexible.

=head4 Void, Scalar, and List Context

Z<amount_context>

X<context; amount>
X<amount context>

One of the axes of context governs I<how many> items you expect.  This is
I<amount context>.  To express this context, evaluate an expression and see
what you do with the results.  One easy way to think of the amount type of
context is to compare it to subject-verb number agreement in English.  You
already know how to do this, even if you don't remember English classes.  If
you can spot the error in the sentence "Perl are a fun language", you've
trained your mind to spot similar types of errors.

X<void context>
X<context; void>

Suppose you have a function called C<some_expensive_operation()> which performs
an expensive calculation and can produce many, many results.  If you call the
function on its own and never use its return value, you've called the function
in I<void context>:

=begin programlisting

    some_expensive_operation();

=end programlisting

X<scalar context>

Assigning the function's return value to a single element evaluates the
function in I<scalar context>:

=begin programlisting

    my $single_result = some_expensive_operation();

=end programlisting

X<list context>

Assigning the results of calling the function to an array or a list, or using
it in a list evaluates the function in I<list context>:

=begin programlisting

    my @all_results        = some_expensive_operation();
    my ($single_element)   = some_expensive_operation();
    process_list_of_results( some_expensive_operation() );

=end programlisting

The second line of the previous example may look confusing; the parentheses
there give a hint to the compiler that although there's only a scalar, this
assignment should occur in list context.  It's semantically equivalent to
assigning to a scalar and a temporary array, and then throwing away the array,
except that no assignment to the array actually occurs:

=begin programlisting

    my ($single_element, @rest) = some_expensive_operation();

=end programlisting

Evaluating a function or expression in list context apart from assignment can
occasionally produce confusion.  Remember that argument lists and lists
themselves--especially lists used in hash initializers--propagate list context
to the expressions they contain.  Thus both of these calls to
C<some_expensive_operation()> are in list context, even if it's not immediately
obvious:

=begin programlisting

    process_list_of_results( some_expensive_operation() );

    my %results =
    (
        cheap_operation     => $cheap_operation_results,
        expensive_operation => some_expensive_operation(), # OOPS!
    );

=end programlisting

X<operators; scalar>
X<scalar>

The latter example probably will not do what you expect, at least if you expect
it to return a single item.  Use the C<scalar> operator to enforce scalar
context on the function call:

=begin programlisting

    my %results =
    (
        cheap_operation     => $cheap_operation_results,
        expensive_operation => scalar some_expensive_operation(),
    );

=end programlisting


=head4 Numeric, String, and Boolean Context

Z<value_contexts>

X<value context>
X<context; value>

Another type of context determines how Perl understands a piece of data--not
I<how many> pieces of data you want, but what the data means.  You've probably
already noticed that Perl's flexible about figuring out if you have a number or
a string and converting between the two as you want them.  This I<value
context> help explain how it does so.

Suppose you want to compare the contents of two strings.  The C<eq> operator
tells you if the strings contain the same information:

=begin programlisting

    say "Catastrophic crypto fail!" if $alice eq $bob;

=end programlisting

You may have had a baffling experience where you I<know> that the strings are
different, but they still compare the same:

=begin programlisting

    my $alice = 'alice';
    say "Catastrophic crypto fail!" if $alice == 'Bob';  # OOPS

=end programlisting

X<string context>
X<numeric context>
X<context; string>
X<context; numeric>

In exchange for not having to declare (or at least track) explicitly what
I<type> of data a variable contains or a function produces, Perl offers
specific type contexts that tell the compiler how to treat a given value during
an operation.  The C<eq> operator treats its operands as strings by enforcing
I<string context> on them.  The C<==> operator enforces I<numeric context>.

X<boolean context>
X<context; boolean>

I<Boolean context> occurs when you use a value in a conditional statement.  In
the previous examples, the C<if> statement evaluated the results of the C<eq>
and C<==> operators in boolean context.

Perl will do its best to coerce values to the proper type (L<coercion>),
depending on the operators you use.  Be sure to use the proper operator for the
type of context you want.

In rare circumstances, you may need to force an explicit context where no
appropriately typed operator exists.  To force a numeric context, add zero to a
variable.  To force a string context, concatenate a variable with the empty
string.  To force a boolean context, double the negation operator:

=begin programlisting

    my $numeric_x =  0 + $x;  # forces numeric context
    my $stringy_x = '' . $x;  # forces string  context
    my $boolean_x =    !!$x;  # forces boolean context

=end programlisting

In general, type contexts are less difficult to understand and see than the
amount contexts.  Once you understand that they exist and know which operators
provide which contexts (L<operator_types>), you'll rarely make mistakes with
them.
